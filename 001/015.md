Faithful Bubblegum Chipmunk

Medium

# Large messages can be permanently be lost due to unaccounted gas costs

### Summary

Large messages can be permanently be lost due to unaccounted dynamic gas costs in the `relayMessage`

### Root Cause

The TRST-M-3 finding in the previous Optimism audit found [here](https://github.com/ethereum-optimism/optimism/blob/develop/docs/security-reviews/2023_12_SuperchainConfigUpgrade_Trust.pdf) highlighted a scenario where large messages over a specific amount of bytes can be lost due to the dynamic gas costs in the `CrossDomainMessenger` which are not accounted for the `baseGas` adjustment. Note that this is not fixed in the Optimism as of yet, however, they are planning to address the issue as noted in the report and should still be addressed.

Firstly, let's take a look at the `baseGas` adjustment:

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/CrossDomainMessenger.sol#L336-L360
```solidity
    /// @notice Computes the amount of gas required to guarantee that a given message will be
    ///         received on the other chain without running out of gas. Guaranteeing that a message
    ///         will not run out of gas is important because this ensures that a message can always
    ///         be replayed on the other chain if it fails to execute completely.
    /// @param _message     Message to compute the amount of required gas for.
    /// @param _minGasLimit Minimum desired gas limit when message goes to target.
    /// @return Amount of gas required to guarantee message receipt.
    function baseGas(bytes calldata _message, uint32 _minGasLimit) public pure returns (uint64) {
        return
        // Constant overhead
        RELAY_CONSTANT_OVERHEAD
        // Calldata overhead
        + (uint64(_message.length) * MIN_GAS_CALLDATA_OVERHEAD)
        // Dynamic overhead (EIP-150)
        + ((_minGasLimit * MIN_GAS_DYNAMIC_OVERHEAD_NUMERATOR) / MIN_GAS_DYNAMIC_OVERHEAD_DENOMINATOR)
        // Gas reserved for the worst-case cost of 3/5 of the `CALL` opcode's dynamic gas
        // factors. (Conservative)
        + RELAY_CALL_OVERHEAD
        // Relay reserved gas (to ensure execution of `relayMessage` completes after the
        // subcontext finishes executing) (Conservative)
        + RELAY_RESERVED_GAS
        // Gas reserved for the execution between the `hasMinGas` check and the `CALL`
        // opcode. (Conservative)
        + RELAY_GAS_CHECK_BUFFER;
    }
```

When we take a look at the `baseGas` adjustment, we see that the final gas meant for `relayMessage` call in `CrossDomainMessenger`, excluding the calldata overhead when the `OptimismPortal` calls the `CrossDomainMessenger` is:
```solidity
285K gas + minGasLimit 
```

Next, let's take a look at the `relayMessage`:
```solidity
    /// @notice Relays a message that was sent by the other CrossDomainMessenger contract. Can only
    ///         be executed via cross-chain call from the other messenger OR if the message was
    ///         already received once and is currently being replayed.
    /// @param _nonce       Nonce of the message being relayed.
    /// @param _sender      Address of the user who sent the message.
    /// @param _target      Address that the message is targeted at.
    /// @param _value       Native token value to send with the message.
    /// @param _minGasLimit Minimum amount of gas that the message can be executed with.
    /// @param _message     Message to send to the target.
    function relayMessage(
        uint256 _nonce,
        address _sender,
        address _target,
        uint256 _value,
        uint256 _minGasLimit,
        bytes calldata _message
    )
        external
        payable
        override
    {
        require(paused() == false, "L1 CrossDomainMessenger: paused");
        require(msg.value == 0, "CrossDomainMessenger: value must be zero");

        (, uint16 _nonceVersion) = Encoding.decodeVersionedNonce(_nonce);
        require(_nonceVersion < 2, "CrossDomainMessenger: only version 0 or 1 messages are supported at this time");

        // If the message is version 0, then it's a migrated legacy withdrawal. We therefore need
        // to check that the legacy version of the message has not already been relayed.
        if (_nonceVersion == 0) {
            bytes32 oldHash = Hashing.hashCrossDomainMessageV0(_target, _sender, _message, _nonce);
            require(successfulMessages[oldHash] == false, "CrossDomainMessenger: legacy withdrawal already relayed");
        }

        // We use the v1 message hash as the unique identifier for the message because it commits
        // to the value and minimum gas limit of the message.
        bytes32 versionedHash =
            Hashing.hashCrossDomainMessageV1(_nonce, _sender, _target, _value, _minGasLimit, _message);

        address _nativeTokenAddress = nativeTokenAddress();
        if (_isOtherMessenger()) {
            // These properties should always hold when the message is first submitted (as
            // opposed to being replayed).
            assert(!failedMessages[versionedHash]);
            if (_value > 0) {
                IERC20(_nativeTokenAddress).safeTransferFrom(address(portal), address(this), _value);
            }
        } else {
            require(failedMessages[versionedHash], "CrossDomainMessenger: message cannot be replayed");
        }

        require(
            _isUnsafeTarget(_target) == false && _target != _nativeTokenAddress,
            "CrossDomainMessenger: cannot send message to blocked system address or nativeTokenAddress"
        );

        require(successfulMessages[versionedHash] == false, "CrossDomainMessenger: message has already been relayed");

        // If there is not enough gas left to perform the external call and finish the execution,
        // return early and assign the message to the failedMessages mapping.
        // We are asserting that we have enough gas to:
        // 1. Call the target contract (_minGasLimit + RELAY_CALL_OVERHEAD + RELAY_GAS_CHECK_BUFFER)
        //   1.a. The RELAY_CALL_OVERHEAD is included in `hasMinGas`.
        // 2. Finish the execution after the external call (RELAY_RESERVED_GAS).
        //
        // If `xDomainMsgSender` is not the default L2 sender, this function
        // is being re-entered. This marks the message as failed to allow it to be replayed.
        if (
            !SafeCall.hasMinGas(_minGasLimit, RELAY_RESERVED_GAS + RELAY_GAS_CHECK_BUFFER)
                || xDomainMsgSender != Constants.DEFAULT_L2_SENDER
        ) {
            failedMessages[versionedHash] = true;
            emit FailedRelayedMessage(versionedHash);

            // Revert in this case if the transaction was triggered by the estimation address. This
            // should only be possible during gas estimation or we have bigger problems. Reverting
            // here will make the behavior of gas estimation change such that the gas limit
            // computed will be the amount required to relay the message, even if that amount is
            // greater than the minimum gas limit specified by the user.
            if (tx.origin == Constants.ESTIMATION_ADDRESS) {
                revert("CrossDomainMessenger: failed to relay message");
            }
            return;
        }
        ...
    }
```

We want to find the minimum length of message data for the `relayMessage` to be possible to revert due to OOG error, assuming a `minGasLimit` of 0, which should still be replayable by the invariant of the `CrossDomainMessenger`

From the audit report, as well as the new code present in the `CrossDomainMessenge`, the minimum gas required for to execute `relayMessage` without reverting is

2 cold SLOADs and 1 cold address CALL = 7K gas
```solidity
        require(paused() == false, "L1 CrossDomainMessenger: paused");
```
1 hashing round which has dynamic gas cost depending for the length of the message.
```solidity
        bytes32 versionedHash =
            Hashing.hashCrossDomainMessageV1(_nonce, _sender, _target, _value, _minGasLimit, _message);
```
1 cold SLOAD = 2K gas
```solidity
            assert(!failedMessages[versionedHash]);
```
1 cold address CALL + 2 cold NZNZ SSTOREs = 17K gas
```solidity
                IERC20(_nativeTokenAddress).safeTransferFrom(address(portal), address(this), _value);
```
1 cold SLOAD = 2K gas
```solidity
        require(successfulMessages[versionedHash] == false, "CrossDomainMessenger: message has already been relayed");
```
1 warm ZNZ SSTORE = 20K gas
```solidity
            failedMessages[versionedHash] = true;
```
 
Altogether 48K gas + hashing costs is the minimum gas required for the `relayMessage`. That means for the address to revert for 0 minGasLimit, the hashing cost would require 285K - 48K = 237K gas for OOG.

Of the components in the hashing costs it includes:
- Keccak operation which costs 6 gas for every 32-bytes of the message
- Memory expansion costs of `abi.encodeWithSignature` which converts the arguments provided in `hashCrossDomainMessageV1` to `bytes` data before starting to hash it. The cost grows quadratically.

The following is the simulation provided in the audit report to estimate the hashing cost:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "forge-std/console.sol";

contract TestDemo {
    function test_demo() public {
        uint256[] memory input = new uint256[](2590);
        for (uint256 i; i < input.length; i++) {
            input[i] = type(uint256).max;
        }
        bytes memory data = abi.encodePacked(input);
        console.log("Data length: ", data.length);
        (bool success,) = address(this).call(abi.encodeWithSelector(this.encodeCrossDomainMessageV1.selector, data));
    }

    function encodeCrossDomainMessageV1(bytes memory _data) public {
        uint size;
        uint offset;
        assembly { offset := _data }
        size = offset + _data.length;
        console.log("Starting memory size: ", size);
        uint256 startingGas = gasleft();
        console.log("Starting gas: ", startingGas);
        bytes memory b = abi.encodeWithSignature("aaaa",_data);
        assembly { offset := b }
        size = offset + b.length;
        bytes32 kec = keccak256(b);
        // bytes memory c = abi.encodeWithSignature("aaaa",_data);
        // assembly { offset := c }
        // size = offset + c.length;
        // kec = keccak256(c);
        console.log("Total gas used: ", startingGas - gasleft());
        console.log("Completed memory size: ", size);
    }
}
```

Running the PoC, we can observe that for a v1 transaction, which is the default, the approximate message length required for the `relayMessage` to revert with OOG is 82880, which is well under the limit of 120000 bytes enforced by both the OP portal and the L2ToL1MessagePasser.
```solidity
Ran 1 test for test/TestDemo.t.sol:TestDemo
[PASS] test_demo() (gas: 1383590)
Logs:
  Data length:  82880
  Starting memory size:  83008
  Starting gas:  1055817409
  Total gas used:  237880
  Completed memory size:  166444
```
Note that for legacy transactions, the message length required is much less, because there will be 2 hashing costs.

With a non-zero gas limit, there can be a buffer of 37000 bytes that can be added to cause the OOG.

### Internal pre-conditions

Large message size (~83K bytes for a v1 transaction, lower for a v0 transaction)

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Messages with large size can be bricked

### PoC

1. User sends message with funds attached with a large size from L2 to L1
2. Malicious user proves and finalizes withdrawal message on L1 providing only the minimum gas required to call the `CrossDomainMessenger.relayMessage` with only the `baseGas` adjusted `minGasLimit` gas.
3. It will revert due to OOG, because it fails to account for the hashing costs of the message.
4. The message is permanently lost as it was not stored in the replay hash mapping on the `CrossDomainMessenger`, leading to a loss of funds.
### Mitigation

_No response_