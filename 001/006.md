Vast Pebble Gorilla

High

# Lack of consideration for external call gas cost when computing min gas amount when sending message

## Summary

## Vulnerability Detail

Description:

When funds are being deposited through the Cross Domain Messenger, we perform the [baseGas()](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/CrossDomainMessenger.sol#L183) function to increase the gas limit to account for the extra gas used while relaying the message. 

Again, the goal is to ensure that if a user specifies a minGasLimit, and their function uses less than that minGasLimit, it should never revert and lose their funds.

```solidity
 function sendMessage(address _target, bytes calldata _message, uint32 _minGasLimit) external payable {
        // Triggers a message to the other messenger. Note that the amount of gas provided to the
        // message is the amount of gas requested by the user PLUS the base gas value. We want to
        // guarantee the property that the call to the target contract will always have at least
        // the minimum gas limit specified by the user.
        _sendMessage({
            _to: address(otherMessenger),
 @           _gasLimit: baseGas(_message, _minGasLimit),
            _value: msg.value,
            _data: abi.encodeWithSelector(
                this.relayMessage.selector, messageNonce(), msg.sender, _target, msg.value, _minGasLimit, _message
            )
        });

        emit SentMessage(_target, msg.sender, _message, messageNonce(), _minGasLimit);
        emit SentMessageExtension1(msg.sender, msg.value);

        unchecked {
            ++msgNonce;
        }
    }
```

However, no such care has been taken for the minGasLimit sending out message.

The result is that the relay message in other chain will use this exact gas amount. This is a problem because some of this gas is used up on the external call:

1. [pulling native token from portal to the messager](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L1CrossDomainMessenger.sol#L257), 
2. [trigger approve external contract](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L1CrossDomainMessenger.sol#L300)
3. [clear approve contract.](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L1CrossDomainMessenger.sol#L305)

After the call itself is performed, the various checks are done, the event is emmitted, and 1/64th of the gas is held behind when the call is made, the gas that is actually passed the call is less than the minGasLimit that was specified.

```solidity
  address _nativeTokenAddress = nativeTokenAddress();
        if (_isOtherMessenger()) {
            // These properties should always hold when the message is first submitted (as
            // opposed to being replayed).
            assert(!failedMessages[versionedHash]);
            if (_value > 0) {
 @               IERC20(_nativeTokenAddress).safeTransferFrom(address(portal), address(this), _value);
            }
        } else {
            require(failedMessages[versionedHash], "CrossDomainMessenger: message cannot be replayed");
        }

        require(
            _isUnsafeTarget(_target) == false && _target != _nativeTokenAddress,
            "CrossDomainMessenger: cannot send message to blocked system address or nativeTokenAddress"
        );

        require(successfulMessages[versionedHash] == false, "CrossDomainMessenger: message has already been relayed");

        // If there is not enough gas left to perform the external call and finish the execution,
        // return early and assign the message to the failedMessages mapping.
        // We are asserting that we have enough gas to:
        // 1. Call the target contract (_minGasLimit + RELAY_CALL_OVERHEAD + RELAY_GAS_CHECK_BUFFER)
        //   1.a. The RELAY_CALL_OVERHEAD is included in `hasMinGas`.
        // 2. Finish the execution after the external call (RELAY_RESERVED_GAS).
        //
        // If `xDomainMsgSender` is not the default L2 sender, this function
        // is being re-entered. This marks the message as failed to allow it to be replayed.
        if (
            !SafeCall.hasMinGas(_minGasLimit, RELAY_RESERVED_GAS + RELAY_GAS_CHECK_BUFFER)
                || xDomainMsgSender != Constants.DEFAULT_L2_SENDER
        ) {
            failedMessages[versionedHash] = true;
            emit FailedRelayedMessage(versionedHash);

            // Revert in this case if the transaction was triggered by the estimation address. This
            // should only be possible during gas estimation or we have bigger problems. Reverting
            // here will make the behavior of gas estimation change such that the gas limit
            // computed will be the amount required to relay the message, even if that amount is
            // greater than the minimum gas limit specified by the user.
            if (tx.origin == Constants.ESTIMATION_ADDRESS) {
                revert("CrossDomainMessenger: failed to relay message");
            }
            return;
        }

        xDomainMsgSender = _sender;
        // _target must not be address(0). otherwise, this transaction could be reverted
        if (_value != 0 && _target != address(0)) {
  @          IERC20(_nativeTokenAddress).approve(_target, _value);
        }
        // _target is expected to perform a transferFrom to collect token
        bool success = SafeCall.call(_target, gasleft() - RELAY_RESERVED_GAS, 0, _message);
        if (_value != 0 && _target != address(0)) {
  @        IERC20(_nativeTokenAddress).approve(_target, 0);
        }
        xDomainMsgSender = Constants.DEFAULT_L2_SENDER;

```

as the we can see the minGasLimit does not account for the addtional transaction cost that needs to be done for

```solidity
if (_value > 0) {
     IERC20(_nativeTokenAddress).safeTransferFrom(address(portal), address(this), _value);
}
```

and

```solidity
      xDomainMsgSender = _sender;
        // _target must not be address(0). otherwise, this transaction could be reverted
        if (_value != 0 && _target != address(0)) {
  @          IERC20(_nativeTokenAddress).approve(_target, _value);
        }
        // _target is expected to perform a transferFrom to collect token
        bool success = SafeCall.call(_target, gasleft() - RELAY_RESERVED_GAS, 0, _message);
        if (_value != 0 && _target != address(0)) {
  @        IERC20(_nativeTokenAddress).approve(_target, 0);
        }
        xDomainMsgSender = Constants.DEFAULT_L2_SENDER;
```

when replaying message.

For any transaction that uses a precise amount of gas, the result will be that the transaction will revert and the funds will be permanently lost.

## Impact

Transaction cannot be finalized / relayed because of insufficient gas.

## Code Snippet

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/CrossDomainMessenger.sol#L183

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L1CrossDomainMessenger.sol#L257

## Tool used

Manual Review

## Recommendation

the recommendation is implement a function similar to the Cross Domain Messenger's baseGas() function to adjust the requested gas amount up sufficiently to ensure that the amount that is left for the actual call is at least as much as the minGasLimit specified.

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/CrossDomainMessenger.sol#L343