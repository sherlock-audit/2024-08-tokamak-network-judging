Mysterious Emerald Elk

Medium

# Deposits from `onApprove()` always reverts at L2 if target is equal to L2 Bridge and it makes native token stuck at L1

### Summary

Thanos L2 native token bridge is Optimism fork with several new features and implementations. `onApprove()` callback function is one of the these new features. It designed for users to use `approveAndCall()` function in order to send their native L1 token to L2 but due to missing validation of `to` address passing in certain situations it's impossible to relay the sent message in L2. 

### Root Cause

In [L1CrossDomainMessenger.sol](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L1CrossDomainMessenger.sol#L106), `onApprove()` callback function is called by the native token address. After, unpacking required informations such as `message` and `minGasLimit` and `to`. It sends message and `_sender` to messager with following lines:

**Note: `sendNativeTokenMessage()`'s first parameter should be message sender and in this configuration user's address sent as sender ( see: [Example `approveAndCall()`](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L2NativeToken.sol#L981C1-L1008C2))**

```solidity
    /// @notice ERC20 onApprove callback
    /// @param _owner    Account that called approveAndCall
    /// @param _amount   Approved amount
    /// @param _data     Data used in OnApprove contract
    function onApprove(
        address _owner, // @audit-info This is the users address please see:  https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L2NativeToken.sol#L981C1-L1008C2
        address,
        uint256 _amount,
        bytes calldata _data
    )
        external
        override
        returns (bool)
    {
        require(msg.sender == address(nativeTokenAddress()), "only accept native token approve callback");
        (address to, uint32 minGasLimit, bytes calldata message) = unpackOnApproveData(_data);
&>      _sendNativeTokenMessage(_owner, to, _amount, minGasLimit, message); // @audit It won't work because _owner should be L1 Bridge
        return true;
    }
```

```solidity
    /// @notice Sends a deposit native token message internally to some target address on the other chain. Note that if
    /// the call
    ///         always reverts, then the message will be unrelayable, and any ETH sent will be
    ///         permanently locked. The same will occur if the target on the other chain is
    ///         considered unsafe (see the _isUnsafeTarget() function).
    /// @param _sender      Sender address.
    /// @param _target      Target contract or wallet address.
    /// @param _amount      Amount of deposit native token.
    /// @param _message     Message to trigger the target address with.
    /// @param _minGasLimit Minimum gas limit that the message can be executed with.
    function _sendNativeTokenMessage(
        address _sender,
        address _target,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _message
    )
        internal
    {
        // Collect native token
        if (_amount > 0) {
            address _nativeTokenAddress = nativeTokenAddress();
            IERC20(_nativeTokenAddress).safeTransferFrom(_sender, address(this), _amount);
            IERC20(_nativeTokenAddress).approve(address(portal), _amount);
        }

        // Triggers a message to the other messenger. Note that the amount of gas provided to the
        // message is the amount of gas requested by the user PLUS the base gas value. We want to
        // guarantee the property that the call to the target contract will always have at least
        // the minimum gas limit specified by the user.
        _sendMessage(
            address(otherMessenger),
            baseGas(_message, _minGasLimit),
            _amount,
            abi.encodeWithSelector(
&>              this.relayMessage.selector, messageNonce(), _sender, _target, _amount, _minGasLimit, _message
            )
        );

        emit SentMessage(_target, _sender, _message, messageNonce(), _minGasLimit);
        emit SentMessageExtension1(_sender, _amount);

        unchecked {
            ++msgNonce;
        }
    }
```
`_sendMessage` function is initialize deposit action in portal and native token amount will be taken after here:

```solidity
    function _sendMessage(address _to, uint64 _gasLimit, uint256 _value, bytes memory _data) internal override {
        require(msg.value == 0, "Deny depositing ETH");
&>      portal.depositTransaction(_to, _value, _value, _gasLimit, false, _data);
    }
```
```solidity
    function _depositTransaction(
        address _sender,
        address _to,
        uint256 _mint,
        uint256 _value,
        uint64 _gasLimit,
        bool _isCreation,
        bytes calldata _data,
        bool _isOnApproveTrigger
    )
        internal
        metered(_gasLimit)
    {
        address _nativeTokenAddress = _nativeToken();

        // Lock token in this contract
        if (_mint > 0) {
&>          IERC20(_nativeTokenAddress).safeTransferFrom(_sender, address(this), _mint);
        }

        if (_isCreation) {
            require(_to == address(0), "OptimismPortal: must send to address(0) when creating a contract");
        }

        // Prevent depositing transactions that have too small of a gas limit. Users should pay
        // more for more resource usage.
        require(_gasLimit >= minimumGasLimit(uint64(_data.length)), "OptimismPortal: gas limit too small");

        // Prevent the creation of deposit transactions that have too much calldata. This gives an
        // upper limit on the size of unsafe blocks over the p2p network. 120kb is chosen to ensure
        // that the transaction can fit into the p2p network policy of 128kb even though deposit
        // transactions are not gossipped over the p2p network.
        require(_data.length <= 120_000, "OptimismPortal: data too large");

        // Transform the from-address to its alias if the caller is a contract.
        address from =
            ((_sender != tx.origin) && !_isOnApproveTrigger) ? AddressAliasHelper.applyL1ToL2Alias(_sender) : _sender;

        // Compute the opaque data that will be emitted as part of the TransactionDeposited event.
        // We use opaque data so that we can update the TransactionDeposited event in the future
        // without breaking the current interface.
        bytes memory opaqueData = abi.encodePacked(_mint, _value, _gasLimit, _isCreation, _data); // @audit-info isOnApprove callback

        // Emit a TransactionDeposited event so that the rollup node can derive a deposit
        // transaction for this deposit.
        emit TransactionDeposited(from, _to, DEPOSIT_VERSION, opaqueData);
    }
```

If target is configured as L2 Bridge address this message cannot be relayed in `relayMessage()` function even user configure message data completely correct on L2 because message sender will be user's wallet address. In default Optimism implementation all the bridge actions send with _sender = L1 Bridge Address. Only way to bridge native token from L1 to L2 sending message through [L2StandardBridge::finalizeBridgeNativeToken()](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/6d4cf9ea730d5b52b622f0b3afd41a35d3eba8a2/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L2/L2StandardBridge.sol#L93). L2 Standard Bridge will revert this transaction because of `onlyOtherBridge()` modifier. 
```solidity
    function finalizeBridgeNativeToken(
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    )
        public
        payable
        override
&>      onlyOtherBridge
    {
        require(paused() == false, "StandardBridge: paused");
        require(msg.value == _amount, "StandardBridge: amount sent does not match amount required");
        require(_to != address(this), "StandardBridge: cannot send to self");
        require(_to != address(messenger), "StandardBridge: cannot send to messenger");

        bool success = SafeCall.call(_to, gasleft(), _amount, hex"");
        require(success, "StandardBridge: Native token transfer failed");

        // Emit the correct events. By default this will be _amount, but child
        // contracts may override this function in order to emit legacy events as well.
        _emitNativeTokenBridgeFinalized(_from, _to, _amount, _extraData); 
    }
```
`onlyOtherBridge` modifier is checking the `xDomainMsgSender` on messenger which is equal to users's wallet address.
```solidity
    modifier onlyOtherBridge() {
        require(
            msg.sender == address(messenger) && messenger.xDomainMessageSender() == address(otherBridge),
            "StandardBridge: function can only be called from the other bridge"
        );
        _;
    }
```
```solidity
// CrossDomainMessenger
        xDomainMsgSender = _sender;
        bool success = SafeCall.call(_target, gasleft() - RELAY_RESERVED_GAS, _value, _message);
```


### Impact

Medium - User funds will be directly stucked on L1, user will lose his/her funds indefinitely.

Also, replaying the message in L2 with `relayMessage()` is also not possible because L2 bridge will never accept message sender. If we change the _sender of the message to L2 bridge, this time it will generate a different version hash which is not failed before and it cannot be replayed.

### Mitigation

While `onApprove()` function, to address shouldn't be L2 bridge address and it should be prevented