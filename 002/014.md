Faithful Bubblegum Chipmunk

Medium

# Address aliasing will not be applied if a contract calls `approveAndCall` directly on the portal allowing for impersonation attacks on L2

### Summary

Address aliasing will not be applied if a contract calls `approveAndCall` directly on the portal allowing for impersonation attacks on L2

### Root Cause

An important feature when a deposit transaction gets executed is **address aliasing** when a contract initiates a deposit transaction. 

From the Optimism specs:

https://docs.optimism.io/chain/differences#address-aliasing

> When transactions are sent from L1 to L2 by an Externally Owned Account, the address of the sender of the transaction on L2 will be set to the address of the sender of the transaction on L1. However, the address of the sender of a transaction on L2 will be different if the transaction was triggered by a smart contract on L1.

> Because of the behavior of the CREATE opcode, it is possible to create a contract on both L1 and on L2 that share the same address but have different bytecode. Even though these contracts share the same address, they are fundamentally two different smart contracts and cannot be treated as the same contract. As a result, the sender of a transaction sent from L1 to L2 by a smart contract cannot be the address of the smart contract on L1 or the smart contract on L1 could act as if it were the smart contract on L2 (because the two contracts share the same address).

> To prevent this sort of impersonation, the sender of a transaction is slightly modified when a transaction is sent from L1 to L2 by a smart contract. Instead of appearing to be sent from the actual L1 contract address, the L2 transaction appears to be sent from an "aliased" version of the L1 contract address. This aliased address is a constant offset from the actual L1 contract address such that the aliased address will never conflict with any other address on L2 and the original L1 address can easily be recovered from the aliased address.

However, in the `OptimismPortal`, we can see there is a new change to how the address aliasing is applied.

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/OptimismPortal2.sol#L547-L549
```solidity
      // Transform the from-address to its alias if the caller is a contract.
        address from =
            ((_sender != tx.origin) && !_isOnApproveTrigger) ? AddressAliasHelper.applyL1ToL2Alias(_sender) : _sender;
```
If the `_sender != tx.origin` implying that the sender is a contract but the `_isOnApproveTrigger` is `true`, the `_sender` will not be aliased.

The `_isOnApproveTrigger` variable will be `true` during an `onApprove` callback.

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/OptimismPortal2.sol#L453-L475
```solidity
    /// @notice ERC20 onApprove callback
    /// @param _owner    Account that called approveAndCall
    /// @param _amount   Approved amount
    /// @param _data     Data used in OnApprove contract
    function onApprove(
        address _owner,
        address,
        uint256 _amount,
        bytes calldata _data
    )
        external
        override
        returns (bool)
    {
        (address to, uint256 value, uint32 gasLimit, bytes calldata message) = unpackOnApproveData(_data);
        if (msg.sender == _nativeToken()) {
            _depositTransaction(_owner, to, _amount, value, gasLimit, to == address(0), message, true);
            return true;
        } else {
            return false;
        }
    }
```

Therefore it is possible for a contract to call `approveAndCall` from the L2 native token onto the OP portal directly to bypass this address aliasing security feature, which will set the `_sender` to the contract address and spoof the `msg.sender` on the deposit transaction on L2.

https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/L2NativeToken.sol#L981-L1007
```solidity
    function approveAndCall(address spender, uint256 amount, bytes memory data) public returns (bool) {
        require(approve(spender, amount));
        _callOnApprove(msg.sender, spender, amount, data);
        return true;
    }

    function _callOnApprove(address owner, address spender, uint256 amount, bytes memory data) internal {
        bytes4 onApproveSelector = OnApprove(spender).onApprove.selector;

        require(
            ERC165Checker._supportsInterface(spender, onApproveSelector),
            "ERC20OnApprove: spender doesn't support onApprove"
        );

        (bool ok, bytes memory res) =
            spender.call(abi.encodeWithSelector(onApproveSelector, owner, spender, amount, data));

        // check if low-level call reverted or not
        require(ok, string(res));

        assembly {
            ok := mload(add(res, 0x20))
        }

        // check if OnApprove.onApprove returns true or false
        require(ok, "ERC20OnApprove: failed to call onApprove");
    }
```

### Internal pre-conditions

n/a

### External pre-conditions

n/a

### Attack Path

n/a

### Impact

The address on the L2 will be unaliased. As the L1 and L2 contract are fundamentally two different smart contracts as mentioned by the Optimism specs, this will allow the L1 contract to impersonate the L2 contract and bypass access control, which may lead to loss of funds and other unintended scenarios.

Furthermore, when developers implement access control checks on the sender of the deposit transaction, they may believe that the address of the contract from the L1 is aliased when it actually is not. Therefore would result in unintentional bugs where the access control will be implemented incorrectly and the deposit transaction will always fail.

### PoC

_No response_

### Mitigation

_No response_