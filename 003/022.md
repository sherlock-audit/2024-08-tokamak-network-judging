Shambolic Quartz Spider

Medium

# Bridge Insolvency Risk from Deflationary or Fee-on-Transfer Tokens

## Summary
The [StandardBridge contract](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/StandardBridge.sol) is vulnerable to insolvency when handling deflationary or fee-on-transfer ERC20 tokens. The bridge incorrectly assumes that the amount of tokens transferred equals the amount specified, without accounting for transfer fees.
## Vulnerability Detail
In functions [_initiateBridgeERC20](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/StandardBridge.sol#L416-L460) and [finalizeBridgeERC20](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/universal/StandardBridge.sol#L335-L363), the bridge uses the `_amount` parameter for accounting without verifying the actual tokens received or sent. For tokens that deduct fees on transfer, this results in the bridge recording more tokens than it actually holds, or sending out more tokens than deducted from its internal balance.
## Impact
Over time, the bridge can become insolvent due to the mismatch between recorded deposits and actual token balances.

## Code Snippet
```solidity
 function finalizeBridgeERC20(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    )
        public
        virtual
        onlyOtherBridge
    {
        require(paused() == false, "StandardBridge: paused");
        if (_isOptimismMintableERC20(_localToken)) {
            require(
                _isCorrectTokenPair(_localToken, _remoteToken),
                "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
            );

            OptimismMintableERC20(_localToken).mint(_to, _amount);
        } else {
            deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] - _amount;
            IERC20(_localToken).safeTransfer(_to, _amount);
        }

        // Emit the correct events. By default this will be ERC20BridgeFinalized, but child
        // contracts may override this function in order to emit legacy events as well.
        _emitERC20BridgeFinalized(_localToken, _remoteToken, _from, _to, _amount, _extraData);
    }
    
function _initiateBridgeERC20(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes memory _extraData
    )
        internal
        virtual
    {
        if (_isOptimismMintableERC20(_localToken)) {
            require(
                _isCorrectTokenPair(_localToken, _remoteToken),
                "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
            );

            OptimismMintableERC20(_localToken).burn(_from, _amount);
        } else {
            IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
            deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
        }

        // Emit the correct events. By default this will be ERC20BridgeInitiated, but child
        // contracts may override this function in order to emit legacy events as well.
        _emitERC20BridgeInitiated(_localToken, _remoteToken, _from, _to, _amount, _extraData);

```

## Tool used

Manual Review

## Recommendation
Modify the contract to use balance checks to determine the actual amount received or sent:
```solidity 
uint256 balanceBefore = IERC20(_localToken).balanceOf(address(this));
IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
uint256 receivedAmount = IERC20(_localToken).balanceOf(address(this)) - balanceBefore;
deposits[_localToken][_remoteToken] += receivedAmount;
```


## Reference
https://github.com/mantlenetworkio/mantle-v2/commit/9bbbb64c3078d4a7d6089d018447df84946d397c#diff-31e71162c044a39bbcbe09177695c1e8513ea010a93cb4cbc78dd17a056463b3R710