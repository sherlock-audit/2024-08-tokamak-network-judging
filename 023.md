Shambolic Quartz Spider

High

# NFTs Can Become Stuck Due to Paused NFT Contracts During Withdrawal


## Summary
The [OptimismPortal2](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/OptimismPortal2.sol) contract, which provides the underlying infrastructure for NFT bridges (L1ERC721Bridge and L2ERC721Bridge), is vulnerable to a flaw that can permanently trap users' NFTs in the bridge. This vulnerability exploits the widespread adoption of 'pause' functionality present in many NFTs today, combined with [OptimismPortal2](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/OptimismPortal2.sol)'s failure to properly handle paused contracts during withdrawals. An attacker can manipulate this flaw to render high-value NFTs irretrievable.


## Vulnerability Detail
Many NFT contracts employs a "pause" functionality to halt transfers temporarily. If an NFT contract is paused during the withdrawal process from L2 to L1, the external call to transfer the NFT fails. However, the [finalizeWithdrawalTransaction](https://github.com/sherlock-audit/2024-08-tokamak-network/blob/main/tokamak-thanos/packages/tokamak/contracts-bedrock/src/L1/OptimismPortal2.sol#L349-L429) function marks the withdrawal as `finalized` before executing the external call and doesn't handle the failure properly. This results in the NFT being stuck in the protocol since the withdrawal cannot be retried.

Scenario:

1. Alice uses an NFT (e.g., CryptoKitties) to bridge from L2 to L1.
2. She initiates the withdrawal process and waits for the challenge period to conclude.
3. Before finalization, the NFT contract is paused by its developers.
4. An attacker or any user exploits the `finalizeWithdrawalTransaction` function using Alice's transaction information.
5. The external call to transfer the NFT fails due to the paused contract.
6. The function marks the withdrawal as completed despite the failure.
7. Alice's NFT becomes stuck in the protocol, and she cannot retrieve it.


## Note on Confirmation
The support for NFT bridging in `OptimismPortal2` was confirmed by the Tokamak Network team. A screenshot of this confirmation is available upon request.
<img width="499" alt="Screenshot 2024-09-22 at 20 33 12" src="https://github.com/user-attachments/assets/f8f50c76-d041-4df0-8f11-c0f99649b574">

## Impact
Users could permanently lose access to their NFTs if the contracts are paused during withdrawal.

## Code Snippet
```solidity
    function finalizeWithdrawalTransaction(Types.WithdrawalTransaction memory _tx) external whenNotPaused {
        finalizeWithdrawalTransactionExternalProof(_tx, msg.sender);
    }


    /// @notice Finalizes a withdrawal transaction, using an external proof submitter.
    /// @param _tx Withdrawal transaction to finalize.
    /// @param _proofSubmitter Address of the proof submitter.
    function finalizeWithdrawalTransactionExternalProof(
        Types.WithdrawalTransaction memory _tx,
        address _proofSubmitter
    )
        public
        whenNotPaused
    {
        // Make sure that the l2Sender has not yet been set. The l2Sender is set to a value other
        // than the default value when a withdrawal transaction is being finalized. This check is
        // a defacto reentrancy guard.
        require(
            l2Sender == Constants.DEFAULT_L2_SENDER, "OptimismPortal: can only trigger one withdrawal per transaction"
        );


        // Compute the withdrawal hash.
        bytes32 withdrawalHash = Hashing.hashWithdrawal(_tx);


        // Check that the withdrawal can be finalized.
        checkWithdrawal(withdrawalHash, _proofSubmitter);


        // Mark the withdrawal as finalized so it can't be replayed.
        finalizedWithdrawals[withdrawalHash] = true;


        address _nativeTokenAddress = _nativeToken();


        // Not allow to call native token contract because users can transfer all token out of the contract
        require(
            _tx.target != _nativeTokenAddress, "Optimism Portal: cannot make a direct call to native token contract"
        );


        // Set the l2Sender so contracts know who triggered this withdrawal on L2.
        l2Sender = _tx.sender;


        if (_tx.value != 0) {
            if (_tx.data.length != 0) {
                IERC20(_nativeTokenAddress).approve(_tx.target, _tx.value);
            } else {
                IERC20(_nativeTokenAddress).safeTransfer(_tx.target, _tx.value);
            }
        }


        // Trigger the call to the target contract. We use a custom low level method
        // SafeCall.callWithMinGas to ensure two key properties
        //   1. Target contracts cannot force this call to run out of gas by returning a very large
        //      amount of data (and this is OK because we don't care about the returndata here).
        //   2. The amount of gas provided to the execution context of the target is at least the
        //      gas limit specified by the user. If there is not enough gas in the current context
        //      to accomplish this, `callWithMinGas` will revert.
        bool success;
        if (_tx.data.length != 0) {
            success = SafeCall.callWithMinGas(_tx.target, _tx.gasLimit, 0, _tx.data);
        } else {
            success = true;
        }


        // Reset approval after a call
        if (_tx.data.length != 0 && _tx.value != 0) {
            IERC20(_nativeTokenAddress).approve(_tx.target, 0);
        }


        // Reset the l2Sender back to the default value.
        l2Sender = Constants.DEFAULT_L2_SENDER;


        // All withdrawals are immediately finalized. Replayability can
        // be achieved through contracts built on top of this contract
        emit WithdrawalFinalized(withdrawalHash, success);


        // Reverting here is useful for determining the exact gas cost to successfully execute the
        // sub call to the target contract if the minimum gas limit specified by the user would not
        // be sufficient to execute the sub call.
        if (!success && tx.origin == Constants.ESTIMATION_ADDRESS) {
            revert GasEstimation();
        }
    }
```
As you might have noticed, the withdrawal is marked as `finalized` before the external call to the target contract.
If the external call fails, the withdrawal remains marked as `finalized`.

## Tool used

Manual Review

## Recommendation
Adjust Finalization Timing: 
Only mark withdrawals as `finalized` after successful execution of the external call. 
Move` finalizedWithdrawals[withdrawalHash] = true`; to occur after the external call confirms success.

## Reference
https://github.com/mantlenetworkio/mantle-v2/blob/release/v1.0.0/technical-documents/security-reviews/Secure3/Mantle_V2_%20Secure3%20Audit%20Report.pdf